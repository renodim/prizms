#3> <> prov:specializationOf <https://github.com/timrdf/prizms/blob/master/bin/dataset/pr-neighborlod/unknown-domain.rq> .

prefix dcterms:    <http://purl.org/dc/terms/>
prefix conversion: <http://purl.org/twc/vocab/conversion/>
prefix prov:       <http://www.w3.org/ns/prov#>

select distinct ?s
where {
  graph ?g {
     ?s ?p [] .        # Any RDF URI node
     filter(isIRI(?s)) 
  }
  filter not exists {  # ... that is not used as a predicate
    graph [] { [] ?s [] }
  }
  filter not exists {  # ... and is not used as a class
    graph [] { [] a ?s }
  }
  filter not exists {  # ... and has not appeared in a previous neighborlod queue
    [] a conversion:NeighborLODDataset;
         dcterms:references ?s .
  }
  filter not exists {  # ... and the graph in which it appears is not something that we gathered.
    graph [] { ?g a conversion:NeighborLODDataset }
  }
  filter not exists {  # ... and the graph in which it appears is not something that we created.
    graph [] { ?g a conversion:LinksetDataset }
  }
  filter( regex(str(?s),'^http'))
  filter(!regex(str(?s),'^http://localhost'))
  filter(!regex(str(?s),'^http://rdf.freebase.com/ns/'))
  filter(?g != <http://www.openlinksw.com/schemas/virtrdf#>)
  filter(?g != <http://www.w3.org/2002/07/owl#>)
  filter(?g != <http://localhost:8890/sparql>)
  # Namespace filters:
}
limit 1000
